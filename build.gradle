buildscript {
    ext {
        kotlin_group = 'org.jetbrains.kotlin'
        kotlin_version = '1.2.71'

        corda_artifactory_url = 'https://ci-artifactory.corda.r3cev.com/artifactory'
        corda_group = 'net.corda'
        corda_release_version = '4.3'

        corda_gradle_plugin_group = 'net.corda.plugins'
        corda_gradle_plugin_version = '5.0.4'

        junit_group = 'org.junit.jupiter'
        junit_version = '5.3.1'

        springboot_group = "org.springframework.boot"
        springboot_release_version = "2.1.6.RELEASE"

        cordapp_platform_version = 5
        cordapp_signing_enabled = true
        cordapp_contract = 'Token Balancer Contract'
        cordapp_workflow = 'Token Balancer Flow'
        cordapp_vendor = 'Corda Open Source'
        cordapp_license = 'Apache License, Version 2.0'
        cordapp_version = 1

        tokens_release_version = '1.1'
        tokens_release_group = 'com.r3.corda.lib.tokens'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "$corda_artifactory_url/corda-releases" }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib' }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib-dev' }
    }

    dependencies {
        classpath "$kotlin_group:kotlin-gradle-plugin:$kotlin_version"
        classpath "$corda_gradle_plugin_group:cordapp:$corda_gradle_plugin_version"
        classpath "$corda_gradle_plugin_group:cordformation:$corda_gradle_plugin_version"
        classpath "$corda_gradle_plugin_group:quasar-utils:$corda_gradle_plugin_version"
    }
}

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "$corda_artifactory_url/corda-releases" }
        maven { url "$corda_artifactory_url/corda-lib" }
        maven { url "https://repo.gradle.org/gradle/libs-releases" }
    }

    apply plugin: 'kotlin'
    apply plugin: 'net.corda.plugins.cordapp'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            languageVersion = "1.2"
            apiVersion = "1.2"
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    dependencies {
        //Joda money dependency
        compile group: 'org.joda', name: 'joda-money', version: '1.0.1'
        compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0'
    }


    jar {
        exclude '**/log4j2*.xml'
    }

    test {
        jvmArgs = ["-ea", "-javaagent:../lib/quasar.jar"]
        maxHeapSize = "1024m"
        useJUnitPlatform()
    }
}
